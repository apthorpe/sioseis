      SUBROUTINE EUREKA (LR,R,G,F,A)
C    THIS ROUTINE IS TAKEN FROM ENDERS ROBINSON'S BOOK "MULTICHANNEL
C  TIME SERIES ANALYSIS WITH DIGITAL COMPUTER PROGRAMS", 1967 HOLDEN-DAY,
C  500 SANSOME ST., SAN FRANCISCO, CA.
C
C    NOTE THAT EUREKA IS IDENTICAL TO SUBROUTINE WEINER IN THE FPS LIBRARY,
C  THEREFORE IT IS NICE FOR COMPATIBILITY!
C
C  LR - LENGTH OF FILTER = M
C  R  - AUTOCORRELATION COEFFICIENTS = R0,R1,R2,....,RM
C  G  - RIGHT-HAND SIDE COEFFICIENTS = G0,G1,G2,....,GM
C  F  - FILTER COEFFICIENTS = F0,F1,F2,....FM
C  A  - PREDICTION ERROR OPERATOR = A0=1,A1,A2,....,AM
      DIMENSION R(LR),G(LR),A(LR),F(LR)
      V=R(1)
      D=R(2)
      A(1)=1.
      F(1)=G(1)/V
      Q=F(1)*R(2)
      IF(LR.EQ.1) RETURN
      DO 4 L=2,LR
      A(L)=-D/V
      IF(L.EQ.2) GO TO 2
      L1=(L-2)/2
      L2=L1+1
      IF(L2.LT.2) GO TO 5
      DO 1 J=2,L2
      HOLD=A(J)
      K=L-J+1
      A(J)=A(J)+A(L)*A(K)
1     A(K)=A(K)+A(L)*HOLD
    5 IF(2*L1.EQ.L-2) GO TO 2
      A(L2+1)=A(L2+1)+A(L)*A(L2+1)
2     V=V+A(L)*D
      F(L)=(G(L)-Q)/V
      L3=L-1
      DO 3 J=1,L3
      K=L-J+1
3     F(J)=F(J)+F(L)*A(K)
      IF(L.EQ.LR) RETURN
      D=0.
      Q=0.
      DO 4 I=1,L
      K=L-I+2
      D=D+A(I)*R(K)
4     Q=Q+F(I)*R(K)
      END
